-- programs ------------------------------------------------

entrypoints Prog ;

Program.   Prog ::= [TopDef] ;

FnDef.	   TopDef ::= Type Ident "(" [Arg] ")" Blk ;

separator nonempty TopDef "" ;

Argument.  Arg ::= Type Ident;

separator  Arg "," ;

-- statements ----------------------------------------------

Block.     Blk ::= "{" [Stmt] "}" ;

separator  Stmt "" ;

Empty.     Stmt ::= ";" ;

BStmt.     Stmt ::= Blk ;

Decl.      Stmt ::= Type [Item] ";" ;

NoInit.    Item ::= Ident ;

Init.      Item ::= Ident "=" Expr ;

separator nonempty Item "," ;

Ass.       Stmt ::= Expr "=" Expr  ";" ;

Incr.      Stmt ::= Ident "++"  ";" ;

Decr.      Stmt ::= Ident "--"  ";" ;

Ret.       Stmt ::= "return" Expr ";" ;

VRet.      Stmt ::= "return" ";" ;

Cond.      Stmt ::= "if" "(" Expr ")" Stmt  ;

CondElse.  Stmt ::= "if" "(" Expr ")" Stmt "else" Stmt  ;

While.     Stmt ::= "while" "(" Expr ")" Stmt ;

For.       Stmt ::= "for" "(" Type Ident ":" Expr ")" Stmt ;

SExp.      Stmt ::= Expr  ";" ;

-- Types ---------------------------------------------------

Dimension.  Dim ::= "[]" ;

Int.       Type1 ::= "int" ;

Doub.      Type1 ::= "double" ;

Bool.      Type1 ::= "boolean" ;

Void.      Type1 ::= "void" ;

Arr.       Type ::= Type1 [Dim] ;

internal StringLit. Type ::= "string" ;

internal   Fun. Type ::= Type "(" [Type] ")" ;

coercions  Type 1 ;
separator  Type "," ;
separator nonempty Dim "";

-- Expressions ---------------------------------------------

-- EIndex is left-recursive. The LHS of EIndex can only be Expr8: EVar / EApp
-- Note: To index a 'new-expr', it needs params so it gets lifted to Expr8.

-- EArrLen can only be Expr7 and above(EArrNew , EApp, EVar, EIndex), since the can return an array.

ExpDimen.       ExpDim ::= "[" Expr "]" ;
--ExpDimenEmpty.  ExpDim ::= Dim ;

EIndex.     Expr8 ::= Expr8 ExpDim ;

EVar.       Expr8 ::= Ident ;

EApp.       Expr8 ::= Ident "(" [Expr] ")" ;

EArrNew.    Expr7 ::= "new" Type [ExpDim] ;

EArrLen.    Expr6 ::= Expr7 "." Ident ;

ELitInt.    Expr6 ::= Integer ;

ELitDoub.   Expr6 ::= Double;

ELitTrue.   Expr6 ::= "true" ;

ELitFalse.  Expr6 ::= "false" ;

EString.    Expr6 ::= String ;

Neg.        Expr5 ::= "-" Expr6 ;

Not.        Expr5 ::= "!" Expr6 ;

EMul.       Expr4 ::= Expr4 MulOp Expr5 ;

EAdd.       Expr3 ::= Expr3 AddOp Expr4 ;

ERel.       Expr2 ::= Expr2 RelOp Expr3 ;

EAnd.       Expr1 ::= Expr2 "&&" Expr1 ;

EOr.        Expr ::= Expr1 "||" Expr ;

internal ETyped. Expr ::= "[" Expr ":" Type "]" ;

coercions  Expr 8 ;

separator  Expr "," ;
separator  ExpDim "" ;

-- operators -----------------------------------------------

Plus.      AddOp ::= "+" ;

Minus.     AddOp ::= "-" ;

Times.     MulOp ::= "*" ;

Div.       MulOp ::= "/" ;

Mod.       MulOp ::= "%" ;

LTH.       RelOp ::= "<" ;

LE.        RelOp ::= "<=" ;

GTH.       RelOp ::= ">" ;

GE.        RelOp ::= ">=" ;

EQU.       RelOp ::= "==" ;

NE.        RelOp ::= "!=" ;

-- comments ------------------------------------------------

comment    "#" ;

comment    "//" ;

comment    "/*" "*/" ;

